buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.19.RELEASE'
	}
}

plugins {
	id 'java'
}

apply plugin: 'org.springframework.boot'

group = 'com.soft.cli'
/*
allprojects {
	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) {
			options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
		}
	}
}
*/
jar {
	baseName = 'oauth-service'
	version = '0.0.1'
}

springBoot {
	mainClass = 'com.soft.cli.oauthservice.OauthServiceApplication'
	executable = true
	buildInfo()
}

sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', 'Edgware.SR5')
}

dependencies {

	compile project(":common-controller-advice")
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.cloud:spring-cloud-starter-bus-amqp')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-web')
	runtime('org.springframework.boot:spring-boot-devtools')
	compileOnly('org.springframework.boot:spring-boot-configuration-processor')
	compileOnly('org.projectlombok:lombok')
	compile('org.springframework.security.oauth:spring-security-oauth2')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-data-rest')
	compile('org.springframework.cloud:spring-cloud-starter-config')
	compile('org.springframework.cloud:spring-cloud-starter-eureka')
	compile('org.springframework.cloud:spring-cloud-starter-hystrix')
	compile("org.springframework.cloud:spring-cloud-starter-feign")
	compile('org.springframework.boot:spring-boot-starter-freemarker')
	compile('org.springframework.boot:spring-boot-starter-hateoas')
	compile("org.springframework.plugin:spring-plugin-core:1.2.0.RELEASE")
	compile("org.springframework.boot:spring-boot-starter-aop")
	compile("com.fasterxml.jackson.datatype:jackson-datatype-hibernate4:2.9.2")
	compile("com.fasterxml.jackson.datatype:jackson-datatype-hppc:2.9.2")
	compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.2')
	compile("com.fasterxml.jackson.datatype:jackson-datatype-json-org:2.9.2")
	compile("mysql:mysql-connector-java:5.1.30")
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

/**
 * Lets you pick Spring Boot profile by system properties, e.g.
 * gradle bootRun -Dspring.profiles.active=dev
 * java -jar -Dspring.profiles.active=dev applicationName.jar
 */

if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
	task pathingJar(type: Jar) {
		dependsOn configurations.runtime
		appendix = 'pathing'

		doFirst {
			manifest {
				attributes 'Class-Path': configurations.runtime.files.collect {
					it.toURL().toString().replaceFirst(/file:\/+/, '/').replaceAll(' ', '%20')
				}.join(' ')
			}
		}
	}

	bootRun {
		addResources = false
		dependsOn pathingJar
		doFirst {
			classpath = files("$buildDir/classes/main", "$buildDir/resources/main", pathingJar.archivePath)
		}
	}
} else {
	bootRun {
		addResources = false
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.5'
}